#include "Common.dsf"
#include "DeferredShadingCommon.dsf"

static float2 arrOffsets[4] = 
{
	float2(-0.75, -0.75),
	float2(-0.25, -0.75),
	float2(0.25, -0.75),
	float2(0.75, -0.75),
};

static const float2 arrBasePos[4] = 
{
	float2(1.0, 1.0),
	float2(1.0, -1.0),
	float2(-1.0, 1.0),
	float2(-1.0, -1.0),
};

static const float2 arrUV[4] = 
{
	float2(1.0, 0.0),
	float2(1.0, 1.0),
	float2(0.0, 0.0),
	float2(0.0, 1.0),
};

static const float4 arrMask[4] = 
{
	float4(1.0, 0.0, 0.0, 0.0),
	float4(0.0, 1.0, 0.0, 0.0),
	float4(0.0, 0.0, 1.0, 0.0),
	float4(0.0, 0.0, 0.0, 1.0),
};

struct VS_OUTPUT
{
    float4 Position	: SV_Position; // vertex position 
    float2 UV		: TEXCOORD0;   // vertex texture coords
	float4 sampMask	: TEXCOORD1;
};

VS_OUTPUT GBufferVisualVS( uint VertexID : SV_VertexID )
{
    VS_OUTPUT Output;

    Output.Position = float4( arrBasePos[VertexID % 4].xy * 0.2 + arrOffsets[VertexID / 4], 0.0, 1.0);
    Output.UV = arrUV[VertexID % 4].xy;
	Output.sampMask = arrMask[VertexID / 4].xyzw;
    
    return Output;    
}

GBufferData UnpackGBuffer(float2 UV)
{
	GBufferData Out;

	float depth = GBufferDepthTexture.Sample( GBufferPointSampler, UV.xy ).x;
	Out.LinearDepth = ConvertZToLinearDepth(depth);
	float4 baseColorSpecInt = GBufferColorSpecIntTexture.Sample( GBufferPointSampler, UV.xy );
	Out.Color = baseColorSpecInt.xyz;
	Out.Shiness = baseColorSpecInt.w;
	Out.Normal = GBufferNormalTexture.Sample( GBufferPointSampler, UV.xy ).xyz;
	Out.Normal = normalize(Out.Normal * 2.0 - 1.0);
	Out.SpecularColor = GBufferSpecPowTexture.Sample( GBufferPointSampler, UV.xy ).xyz;

	return Out;
}

float4 GBufferVisualPS( VS_OUTPUT In ) : SV_TARGET
{
	GBufferData gbd = UnpackGBuffer(In.UV.xy);
	float4 finalColor = float4(0.0, 0.0, 0.0, 1.0);
	//finalColor += float4(gbd.LinearDepth,gbd.LinearDepth,gbd.LinearDepth, 0.0) * In.sampMask.xxxx;
	finalColor += float4(1.0 - saturate(gbd.LinearDepth / 1.0), 1.0 - saturate(gbd.LinearDepth / 2.0), 1.0 - saturate(gbd.LinearDepth / 3.0), 0.0) * In.sampMask.xxxx;
	finalColor += float4(gbd.Color.xyz, 0.0) * In.sampMask.yyyy;
	finalColor += float4(gbd.Normal.xyz * 0.5 + 0.5, 0.0) * In.sampMask.zzzz;
	finalColor += float4(gbd.SpecularColor, gbd.Shiness) * In.sampMask.wwww;

	return finalColor;
}
